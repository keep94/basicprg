10 LET C = 7
15 COLOR C, 0
20 LET LMT = 252
30 DIM NAM$(LMT, 7)
40 DIM COL$(7)
50 DIM COL(7)
60 FOR X = 1 TO 6
70 READ COL(X), COL$(X)
80 NEXT X
85 LET COL$(1) = CHR$(251)
87 LET COL(7) = 68
90 LET N = 1
100 LET SLIN = 3
110 LET LIN = 1
120 LET COL = 1
130 LET SC = 1
135 KEY OFF
140 WIDTH 80
142 CLS
145 COLOR C
150 LOCATE 12, 1: INPUT "ENTER BALANCE FORWARD: ", NAM$(1, 7)
155 LET NAM$(1, 7) = " " + NAM$(1, 7)
160 LET BALF = VAL(NAM$(1, 7))
170 NAM$(0, 7) = STR$(BALF)
200 '   SUB SETUP
220 CLS
250 GOSUB 800' SUB SCREEN
260 GOSUB 900' SUB HIGHLIGHT
300 '   SUB INPUT
310 LET X$ = INKEY$
320 IF X$ = "" THEN 310
330 IF X$ = CHR$(0) + CHR$(72) THEN GOSUB 1100: GOTO 310
340 IF X$ = CHR$(0) + CHR$(75) THEN GOSUB 1200: GOTO 310
350 IF X$ = CHR$(0) + CHR$(77) THEN GOSUB 1300: GOTO 310
360 IF X$ = CHR$(0) + CHR$(80) THEN GOSUB 1400: GOTO 310
363 IF X$ = CHR$(0) + "Q" THEN GOLIN = LIN + 18: GOSUB 2738: GOTO 310
366 IF X$ = CHR$(0) + "I" THEN GOLIN = LIN - 18: GOSUB 2738: GOTO 310
370 IF X$ = CHR$(6) THEN GOSUB 1800: GOTO 310
375 IF X$ = CHR$(5) THEN GOSUB 3200: GOTO 310
380 IF X$ = CHR$(12) THEN GOSUB 2000: GOTO 310
390 IF X$ = CHR$(0) + CHR$(83) THEN GOSUB 2200: GOTO 310
400 IF X$ = CHR$(18) THEN GOSUB 2300: GOSUB 2400: GOTO 310
410 IF X$ = CHR$(13) THEN 310
420 IF X$ = CHR$(20) THEN GOSUB 2500: GOTO 310
430 IF X$ = CHR$(7) THEN GOSUB 2700: GOTO 310
440 IF X$ = CHR$(16) THEN GOSUB 2800: GOTO 310
450 IF X$ = CHR$(9) THEN GOSUB 3300: GOTO 310
460 IF X$ = CHR$(14) THEN EXT = 1: GOSUB 3330: GOTO 310
465 IF X$ = CHR$(4) THEN GOSUB 3500: GOTO 310
470 IF X$ = CHR$(25) THEN EXT = 1: GOSUB 3530: GOTO 310
480 GOSUB 500
490 GOTO 310
500 '   SUB INPUT
510 IF COL = 5 OR COL = 6 THEN LET AM = VAL(NAM$(LIN, COL))
520 IF LIN > N THEN GOSUB 1500
530 COLOR C + 8, 0: LOCATE 23, 1, 1: PRINT LIN; " "; COL$(COL); ": "; X$;
535 IF LEN(X$) = COL(COL + 1) - COL(COL) - 4 THEN 640
540 LET X1$ = INKEY$
550 IF X1$ = "" THEN 540
560 IF X1$ = CHR$(13) THEN 640
565 IF X1$ = CHR$(8) AND LEN(X$) = 0 THEN 540
570 IF X1$ = CHR$(8) THEN 610
580 PRINT X1$;
590 LET X$ = X$ + X1$
600 GOTO 535
610 LET X1 = LEN(X$)
620 LET X$ = MID$(X$, 1, X1 - 1)
625 LOCATE 23, 1: PRINT SPACE$(30);
630 GOTO 530
640 LOCATE 23, 1: PRINT SPACE$(30);
645 LET NAM$(LIN, COL) = X$
650 GOSUB 900
660 IF COL < 5 THEN RETURN
670 LET AM = VAL(NAM$(LIN, COL)) - AM
680 IF COL = 5 THEN LET AM = -AM
690 GOSUB 1700' SUB CORRECT
695 GOSUB 800' SUB SCREEN
697 GOSUB 900' SUB HIGHLIGHT
700 RETURN
800 '   SUB SCREEN
802 COLOR C, 0
805 CLS
807 GOSUB 1600
810 FOR Z = SC * 18 - 17 TO SC * 18
820 LOCATE Z - 18 * (INT((Z - 1) / 18)) + 2, 1: PRINT Z;
830 FOR ZA = 1 TO 7
840 LOCATE , COL(ZA): PRINT NAM$(Z, ZA);
850 NEXT ZA
860 NEXT Z
870 RETURN
900 '   SUB HIGHLIGHT
910 LET NAM = LEN(NAM$(LIN, COL))
920 COLOR 0, C: LOCATE SLIN, COL(COL): PRINT NAM$(LIN, COL) + SPACE$(COL(COL + 1) - COL(COL) - NAM - 4)
930 RETURN
1000 '   SUB DONT HIGHLIGHT
1005 LET NAM = LEN(NAM$(LIN, COL))
1010 COLOR C, 0: LOCATE SLIN, COL(COL): PRINT NAM$(LIN, COL) + SPACE$(COL(COL + 1) - COL(COL) - NAM - 4)
1020 RETURN
1100 '   SUB UP
1110 IF LIN = 1 THEN RETURN
1120 GOSUB 1000' SUB DONT HIGHLIGHT
1130 LET LIN = LIN - 1
1140 IF LIN < SC * 18 - 17 THEN LET SC = SC - 1: GOSUB 800' SUB SCREEN
1150 LET SLIN = LIN - SC * 18 + 20
1160 GOSUB 900' SUB HIGHLIGHT
1170 RETURN
1200 '   SUB LEFT
1210 GOSUB 1000' SUB DONT HIGHLIGHT
1220 LET COL = COL - 1
1230 IF COL = 0 THEN LET COL = 6
1240 GOSUB 900' SUB HIGHLIGHT
1250 RETURN
1300 '   SUB RIGHT
1310 GOSUB 1000' SUB DONT HIGHLIGHT
1320 LET COL = COL + 1
1330 IF COL = 7 THEN LET COL = 1
1340 GOSUB 900' SUB HIGHLIGHT
1350 RETURN
1400 '   SUB DOWN
1410 IF LIN = LMT THEN RETURN
1420 GOSUB 1000' SUB DONT HIGHLIGHT
1430 LET LIN = LIN + 1
1440 IF LIN > SC * 18 THEN LET SC = SC + 1: GOSUB 800' SUB SCREEN
1450 LET SLIN = LIN - SC * 18 + 20
1460 GOSUB 900' SUB HIGHLIGHT
1470 RETURN
1500 '   SUB INCREASE
1510 FOR Z = N TO LIN
1520 LET NAM$(Z, 7) = NAM$(N, 7)
1530 NEXT Z
1540 GOSUB 800' SUB SCREEN
1550 GOSUB 900' SUB HIGHLIGHT
1560 LET N = LIN
1570 RETURN
1600 '   SUB HEADING
1610 LOCATE 1, 6: PRINT CHR$(251) + SPACE$(4) + "NO. " + SPACE$(4) + "DATE " + SPACE$(4) + "NAME        " + SPACE$(4) + "WITH.   " + SPACE$(4) + "DEP.    " + SPACE$(4) + "BAL.    "
1620 RETURN
1700 '   SUB CORRECT
1710 FOR Z = LIN TO N
1720 LET NAM$(Z, 7) = STR$(CSNG(VAL(NAM$(Z, 7)) + AM))
1730 NEXT Z
1740 RETURN
1800 '   SUB SAVE
1810 COLOR C + 8, 0
1820 LOCATE 23, 1: INPUT "SAVE FILE: ", X$
1825 IF X$ = "" THEN LOCATE 23, 1: PRINT SPACE$(70): RETURN
1830 OPEN X$ FOR OUTPUT AS #1
1835 PRINT #1, BALF
1840 FOR Z = 1 TO N
1850 FOR Z2 = 1 TO 7
1860 PRINT #1, NAM$(Z, Z2)
1870 NEXT Z2
1880 NEXT Z
1890 CLOSE #1
1900 LOCATE 23, 1: PRINT SPACE$(30);
1910 RETURN
2000 '   SUB LOAD
2010 COLOR C + 8, 0
2020 LOCATE 23, 1: INPUT "LOAD FILE: ", X$
2022 IF X$ = "" THEN LOCATE 23, 1: PRINT SPACE$(70): RETURN
2025 ERASE NAM$
2026 DIM NAM$(LMT, 7)
2030 OPEN X$ FOR INPUT AS #1
2035 INPUT #1, BALF
2037 LET NAM$(0, 7) = STR$(BALF)
2040 FOR Z = 1 TO LMT
2050 FOR Z2 = 1 TO 7
2060 LINE INPUT #1, NAM$(Z, Z2)
2065 IF EOF(1) THEN 2090
2070 NEXT Z2
2080 NEXT Z
2090 CLOSE #1
2100 LET N = Z
2110 LET SLIN = 3
2120 LET LIN = 1
2130 LET COL = 1
2140 LET SC = 1
2150 GOSUB 800' SUB SCREEN
2160 GOSUB 900' SUB HIGHLIGHT
2170 RETURN
2200 '   SUB DELETE
2210 LET AM = VAL(NAM$(LIN, COL))
2220 LET NAM$(LIN, COL) = ""
2230 GOSUB 900
2240 IF COL < 5 THEN RETURN
2250 IF COL = 6 THEN LET AM = -AM
2260 GOSUB 1700' SUB CORRECT
2270 GOSUB 800' SUB SCREEN
2280 GOSUB 900' SUB HIGHLIGHT
2290 RETURN
2300 '   SUB BALANCE
2305 COLOR C + 8, 0
2310 LET CBAL = BALF
2320 FOR Z = 1 TO N
2330 IF NAM$(Z, 1) <> "Y" THEN 2350
2340 LET CBAL = CBAL - VAL(NAM$(Z, 5)) + VAL(NAM$(Z, 6))
2350 NEXT Z
2360 RETURN
2400 '   SUB PRINT BALANCE
2410 LOCATE 23, 50: PRINT "BAL:"; CBAL
2420 LET Z2$ = INKEY$
2430 IF Z2$ = "" THEN 2420
2440 LOCATE 23, 50: PRINT SPACE$(28)
2450 RETURN
2500 '   SUB TRANSFER
2510 COLOR C + 8, 0
2520 LOCATE 23, 1: INPUT "TRANSFER TO FILE: ", X$
2525 IF X$ = "" THEN LOCATE 23, 1: PRINT SPACE$(70): RETURN
2527 GOSUB 2300
2530 LET BALF2 = CBAL
2540 OPEN X$ FOR OUTPUT AS #1
2550 PRINT #1, BALF2
2560 FOR Z = 1 TO N
2570 IF NAM$(Z, 1) = "Y" THEN 2640
2580 FOR Z2 = 1 TO 6
2590 PRINT #1, NAM$(Z, Z2)
2600 NEXT Z2
2610 LET ITCOL7$ = STR$(INT(BALF2 * 100 - VAL(NAM$(Z, 5)) * 100 + VAL(NAM$(Z, 6)) * 100) / 100)
2620 PRINT #1, ITCOL7$
2630 LET BALF2 = VAL(ITCOL7$)
2640 NEXT Z
2641 IF Z = LMT + 1 THEN 2650
2642 LET ITCOL7$ = STR$(INT(BALF2 * 100) / 100)
2644 FOR Z2 = 1 TO 6
2645 PRINT #1, CHR$(13) + CHR$(10);
2646 NEXT Z2
2647 PRINT #1, ITCOL7$
2650 CLOSE #1
2660 LOCATE 23, 1: PRINT SPACE$(75)
2670 RETURN
2700 '   SUB GOTO
2710 COLOR C + 8, 0
2720 LOCATE 23, 1: INPUT "GO TO LINE: ", X$
2730 LET GOLIN = VAL(X$)
2735 LET GOLIN = INT(GOLIN)
2738 COLOR C + 8, 0
2740 IF GOLIN <= 0 OR GOLIN > LMT THEN LOCATE 23, 1: PRINT SPACE$(30): RETURN
2745 LET LIN = GOLIN
2750 LET SC = INT((LIN - 1) / 18) + 1
2760 LET SLIN = LIN - SC * 18 + 20
2762 GOSUB 800
2765 GOSUB 900
2770 RETURN
2800 '   SUB PRINT
2810 COLOR C + 8, 0
2820 LOCATE 23, 1: PRINT "Turn on printer strike a key when ready."
2830 LET READY$ = INKEY$
2835 IF READY$ = "" THEN 2830
2837 IF READY$ = CHR$(27) THEN 3100
2840 LOCATE 23, 1: PRINT "Press ESC to abort print"
2850 FOR CNT5 = 1 TO 5
2860 LPRINT
2870 NEXT CNT5
2872 LPRINT "     " + CHR$(251) + SPACE$(4) + "NO.     " + "DATE     " + "NAME            " + "WITH.       " + "DEP.        " + "BAL."
2873 LPRINT
2875 LET CNT6 = 0
2880 FOR CNT5 = 1 TO N
2890 LET READY$ = INKEY$: IF READY$ = CHR$(27) THEN 3100
2900 IF CNT6 = 55 THEN GOSUB 3000
2910 LET CNT5$ = STR$(CNT5)
2920 LPRINT SPACE$(4 - LEN(CNT5$)) + CNT5$ + SPACE$(2);
2930 FOR CNT7 = 1 TO 6
2940 LPRINT NAM$(CNT5, CNT7) + SPACE$(COL(CNT7 + 1) - COL(CNT7) - LEN(NAM$(CNT5, CNT7)));
2950 NEXT CNT7
2960 LPRINT NAM$(CNT5, 7)
2970 LET CNT6 = CNT6 + 1
2980 NEXT CNT5
2990 GOTO 3100
3000 '   SUB SKIP
3005 LET CNT6 = 0
3010 FOR CNT7 = 1 TO 11
3020 LPRINT
3030 NEXT CNT7
3040 RETURN
3100 '   SUB TERMINATE PRINT
3110 LOCATE 23, 1: PRINT SPACE$(70)
3120 RETURN
3200 '   sub exit
3210 COLOR C + 8, 0
3220 LOCATE 23, 1: INPUT "ARE YOU SURE <Y,N>? ", EXT$
3230 IF EXT$ = "Y" OR EXT$ = "y" THEN COLOR 7, 0: CLS : END
3240 LOCATE 23, 1: PRINT SPACE$(70)
3250 RETURN
3300 '   sub insert
3310 COLOR C + 8, 0
3320 LOCATE 23, 1: INPUT "INSERT HOW MANY LINES: ", EXT
3330 IF EXT > LMT - N THEN COLOR C + 8, 0: LOCATE 23, 1: PRINT SPACE$(50): RETURN
3340 FOR CNT7 = N TO LIN STEP -1
3350 FOR CNT6 = 1 TO 7
3360 LET NAM$(CNT7 + EXT, CNT6) = NAM$(CNT7, CNT6)
3370 NEXT CNT6
3380 NEXT CNT7
3390 FOR CNT7 = LIN TO LIN + EXT - 1
3400 FOR CNT6 = 1 TO 6
3410 LET NAM$(CNT7, CNT6) = ""
3420 NEXT CNT6
3430 LET NAM$(CNT7, 7) = NAM$(LIN - 1, 7)
3440 NEXT CNT7
3450 LET N = N + EXT
3470 GOSUB 800
3480 GOSUB 900
3490 RETURN
3500 '   SUB DELETE
3510 COLOR C + 8, 0
3520 LOCATE 23, 1: INPUT "DELETE HOW MANY LINES: ", EXT
3530 IF EXT > N - LIN + 1 THEN COLOR C + 8, 0: LOCATE 23, 1: PRINT SPACE$(50): RETURN
3537 AM = 0
3540 FOR CNT7 = LIN TO LIN + EXT - 1
3550 AM = AM + VAL(NAM$(CNT7, 5)) - VAL(NAM$(CNT7, 6))
3560 NEXT CNT7
3570 FOR CNT7 = LIN TO N - EXT
3580 FOR CNT6 = 1 TO 7
3590 LET NAM$(CNT7, CNT6) = NAM$(CNT7 + EXT, CNT6)
3600 NEXT CNT6
3610 NEXT CNT7
3620 FOR CNT7 = N - EXT + 1 TO N
3630 FOR CNT6 = 1 TO 7
3640 LET NAM$(CNT7, CNT6) = ""
3650 NEXT CNT6
3660 NEXT CNT7
3670 LET N = N - EXT
3680 GOSUB 1700
3690 GOSUB 800
3700 GOSUB 900
3710 RETURN
5000 '   SUB DATA
5010 DATA 6,CHR$(251)
5020 DATA 11,"NO."
5030 DATA 19,"DATE"
5040 DATA 28,"NAME"
5050 DATA 44,"WITH."
5060 DATA 56,"DEP."

