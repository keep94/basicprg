10 DIM NAM$(594)
20 DIM FIELDS(40)
30 DIM FIELDS$(40)
40 DIM ORD(40)
45 DIM SRCH$(40)
50 KEY OFF
200 '   sub menu
205 CLS
210 LOCATE 6,10:PRINT "1.  Load file"
220 LOCATE 8,10:PRINT "2.  Save file"
230 LOCATE 10,10:PRINT "3.  List file"
240 LOCATE 12,10:PRINT "4.  Search file"
250 LOCATE 14,10:PRINT "5.  Sort file"
260 LOCATE 17,1:INPUT "Pick one: ",CH$
270 IF CH$="1" THEN GOSUB 500
280 IF CH$="2" THEN GOSUB 1000
290 IF CH$="3" THEN POSIT=0:GOSUB 1100
300 IF CH$="4" THEN GOSUB 1300
310 IF CH$="5" THEN GOSUB 1600
320 GOTO 200
500 '   sub load
505 GOSUB 2300
510 CLS
520 LOCATE 12,1:INPUT"Enter file you wish to load: ",S$
525 ERASE NAM$
527 DIM NAM$(594)
530 OPEN S$ FOR INPUT AS #1
550 GOSUB 700
570 COUNT=0
580 IF EOF(1) THEN 620
590 COUNT=COUNT+1
600 LINE INPUT #1,NAM$(COUNT)
610 GOTO 580
620 CLOSE #1
625 SIZE=COUNT
630 RETURN
700 '   sub format file
720 CNT=1
730 LINE INPUT #1,NAM$(0)
740 FOR COUNT=1 TO LEN(NAM$(0))
750 Y$=MID$(NAM$(0),COUNT,1)
760 IF Y$<>" " THEN 780
770 NEXT COUNT
775 NFIELDS=0:RETURN
780 FIELDS(CNT)=COUNT
790 FOR COUNT=FIELDS(CNT) TO LEN(NAM$(0))
795 IF COUNT=161 THEN NFIELDS=CNT:FIELDS(CNT+1)=161:RETURN
800 Y$=MID$(NAM$(0),COUNT,1)
810 IF Y$=":" THEN 840
820 FIELDS$(CNT)=FIELDS$(CNT)+Y$
830 NEXT COUNT
835 NFIELDS=CNT:FIELDS(CNT+1)=161:RETURN
840 IF COUNT=LEN(NAM$(0)) THEN NFIELDS=CNT:FIELDS(CNT+1)=161:RETURN
845 CNT=CNT+1
850 FIELDS(CNT)=COUNT+1
860 IF MID$(NAM$(0),FIELDS(CNT),1)<>" " THEN 790
870 IF FIELDS(CNT)>LEN(NAM$(0))-1 THEN NFIELDS=CNT-1:FIELDS(CNT)=161:RETURN
880 IF FIELDS(CNT)>159 THEN NFIELDS=CNT-1:FIELDS(CNT)=161:RETURN
890 FIELDS(CNT)=FIELDS(CNT)+1
900 GOTO 860
1000 '   sub save
1010 CLS
1020 LOCATE 12,1:INPUT"Save under what file: ",S$
1025 IF S$="" THEN RETURN
1030 OPEN S$ FOR OUTPUT AS #1
1040 FOR COUNT=0 TO SIZE
1050 PRINT #1,NAM$(COUNT)
1060 NEXT COUNT
1070 CLOSE #1
1080 RETURN
1100 '   sub list
1110 SCN=0
1115 SCN1=1
1120 CLS
1130 PRINT MID$(NAM$(0),SCN1,SCN1+79)
1135 PRINT
1140 FOR COUNT=SCN*18 + 1 TO SCN*18 + 18
1145 IF COUNT=POSIT THEN COLOR 0,7:PRINT MID$(NAM$(COUNT),SCN1,SCN1+79):COLOR 7,0:GOTO 1160
1150 PRINT MID$(NAM$(COUNT),SCN1,SCN1+79)
1160 NEXT COUNT
1170 RE$=""
1180 WHILE LEN(RE$)<1:RE$=INKEY$:WEND
1190 IF RE$=CHR$(0)+CHR$(72) THEN IF SCN>0 THEN SCN=SCN-1:GOTO 1120
1192 IF RE$<>CHR$(0)+CHR$(75) AND RE$<>CHR$(0)+CHR$(77) THEN 1200
1193 IF SCN1=1 THEN SCN1=81:GOTO 1120
1194 IF SCN1=81 THEN SCN1=1:GOTO 1120
1200 IF RE$=CHR$(0)+CHR$(80) THEN IF SCN<32 AND SCN*18<SIZE-18 THEN SCN=SCN+1:GOTO 1120
1210 IF RE$=CHR$(27) THEN RETURN
1220 GOTO 1170
1300 '   sub search
1305 SCN=0
1310 CLS
1315 PRINT "Fields:"
1317 PRINT
1320 FOR COUNT=1 TO NFIELDS
1330 PRINT COUNT;FIELDS$(COUNT)
1340 NEXT COUNT
1350 PRINT
1360 LOCATE 12,1:INPUT"Search in which field: ",CH
1362 IF CH=0 THEN RETURN
1365 IF CH>NFIELDS OR CH<0 THEN LOCATE 12,1:PRINT SPACE$(30):GOTO 1360
1370 CLS
1373 LOCATE 12,1:INPUT "Wild card search <y,n>: ",WILD$
1376 CLS
1380 LOCATE 12,1:PRINT "Enter entry to search for in field "+FIELDS$(CH)+": ";
1390 LINE INPUT X$
1400 FOR CNT=1 TO SIZE
1410 ENTRY$=MID$(NAM$(CNT),FIELDS(CH),FIELDS(CH+1)-FIELDS(CH))
1412 IF ENTRY$=SPACE$(LEN(ENTRY$)) THEN 1470
1415 IF WILD$<>"Y" AND WILD$<>"y" THEN 1426
1420 GOSUB 1500
1423 GOTO 1430
1426 EQ=0
1427 GOSUB 2400
1428 IF ENTRY$=X$ THEN EQ=1
1430 IF EQ=0 THEN 1470
1440 POSIT=CNT
1450 SCN=INT((CNT-1)/18)
1460 GOSUB 1115
1470 NEXT CNT
1480 RETURN
1500 '   sub wildcard
1505 EQ=1
1510 GOSUB 2400
1515 POSIT=0
1520 DONE=0
1522 COUNT=1
1525 POSIT1=POSIT
1530 POSIT=INSTR(POSIT1+1,X$,"*")
1532 IF POSIT=0 THEN POSIT=LEN(X$)+1:DONE=1
1535 SRCH$(COUNT)=MID$(X$,POSIT1+1,POSIT-POSIT1-1)
1537 IF DONE THEN 1548
1540 COUNT=COUNT+1
1545 GOTO 1525
1548 NSRCH=COUNT
1549 FOR COUNT=1 TO LEN(SRCH$(1))
1550 IF MID$(SRCH$(1),COUNT,1)="?" THEN 1552
1551 IF MID$(SRCH$(1),COUNT,1)<>MID$(ENTRY$,COUNT,1) THEN EQ=0:RETURN
1552 NEXT COUNT
1554 COUNT1=LEN(SRCH$(1))
1555 FOR COUNT=2 TO NSRCH - 1
1556 IF COUNT1>LEN(ENTRY$)-LEN(SRCH$(COUNT)) THEN EQ=0:RETURN
1557 COUNT1=COUNT1+1
1558 COUNT3=COUNT1
1560 FOR COUNT2=1 TO LEN(SRCH$(COUNT))
1563 IF MID$(SRCH$(COUNT),COUNT2,1)="?" THEN COUNT3=COUNT3+1:GOTO 1570
1565 IF MID$(SRCH$(COUNT),COUNT2,1)<>MID$(ENTRY$,COUNT3,1) THEN 1556
1568 COUNT3=COUNT3+1
1570 NEXT COUNT2
1575 COUNT1=COUNT3-1
1580 NEXT COUNT
1582 COUNT1=LEN(ENTRY$)
1584 FOR COUNT=LEN(SRCH$(NSRCH)) TO 1 STEP -1
1585 IF MID$(SRCH$(NSRCH),COUNT,1)="?" THEN COUNT1=COUNT1-1:GOTO 1588
1586 IF MID$(SRCH$(NSRCH),COUNT,1)<>MID$(ENTRY$,COUNT1,1) THEN EQ=0:RETURN
1587 COUNT1=COUNT1-1
1588 NEXT COUNT
1590 RETURN
1600 '   sub sort
1610 CLS
1620 LOCATE 12,1:INPUT "Sort in (a)scending or (d)escending order? ",O$
1630 CLS
1640 PRINT "Fields: "
1650 PRINT
1660 FOR COUNT=1 TO NFIELDS
1670 PRINT COUNT;FIELDS$(COUNT)
1680 NEXT COUNT
1690 COUNT=1
1700 LOCATE 12,1:PRINT "Enter fields";COUNT;
1710 INPUT ORD(COUNT)
1715 LOCATE 12,1:PRINT SPACE$(30)
1720 IF ORD(COUNT)=-1 THEN 1740
1721 IF ORD(COUNT)=0 THEN RETURN
1722 IF ORD(COUNT)>NFIELDS OR ORD(COUNT)<1 THEN 1700
1725 COUNT=COUNT+1
1727 IF COUNT>NFIELDS THEN 1740
1730 GOTO 1700
1740 CLS:LOCATE 12,1:PRINT "Wait. . ."
1750 INTER=1
1760 INTER=INTER*3 + 1
1770 IF INTER>SIZE THEN 1790
1780 GOTO 1760
1790 INTER=INT(INT(INTER/3)/3)
1800 IF INTER=0 THEN INTER=1
1810 FOR CNT=1 TO INTER
1820 POSIT=CNT
1830 POSIT=POSIT+INTER
1840 IF POSIT>SIZE THEN 2010
1850 POSIT$=NAM$(POSIT)
1860 EXAM=POSIT-INTER
1870 GOSUB 2200
1880 IF O$="d" OR O$="D" THEN 1910
1890 IF ISG>-1 THEN 1950
1900 GOTO 1920
1910 IF ISG<1 THEN 1950
1920 EXAM=EXAM-INTER
1930 IF EXAM<1 THEN ST=CNT:GOTO 1960
1940 GOTO 1870
1950 ST=EXAM+INTER
1960 FOR CNT1=POSIT-INTER TO ST STEP -INTER
1970 NAM$(CNT1+INTER)=NAM$(CNT1)
1980 NEXT CNT1
1990 NAM$(ST)=POSIT$
2000 GOTO 1830
2010 NEXT CNT
2020 IF INTER=1 THEN RETURN
2030 INTER=INT(INTER/3)
2040 GOTO 1810
2200 '   sub is greater than
2210 COUNT=1
2215 IF ORD(COUNT)=-1 THEN ISG=0:RETURN
2220 IF MID$(POSIT$,FIELDS(ORD(COUNT)),FIELDS(ORD(COUNT)+1)-FIELDS(ORD(COUNT)))=MID$(NAM$(EXAM),FIELDS(ORD(COUNT)),FIELDS(ORD(COUNT)+1)-FIELDS(ORD(COUNT))) THEN COUNT=COUNT+1:GOTO 2215
2230 IF MID$(POSIT$,FIELDS(ORD(COUNT)),FIELDS(ORD(COUNT)+1)-FIELDS(ORD(COUNT)))>MID$(NAM$(EXAM),FIELDS(ORD(COUNT)),FIELDS(ORD(COUNT)+1)-FIELDS(ORD(COUNT))) THEN ISG=1:RETURN
2240 IF MID$(POSIT$,FIELDS(ORD(COUNT)),FIELDS(ORD(COUNT)+1)-FIELDS(ORD(COUNT)))<MID$(NAM$(EXAM),FIELDS(ORD(COUNT)),FIELDS(ORD(COUNT)+1)-FIELDS(ORD(COUNT))) THEN ISG=-1:RETURN
2300 '   sub initialize fields
2310 FOR COUNT=1 TO 40
2320 FIELDS$(COUNT)=""
2330 NEXT COUNT
2340 RETURN
2400 '   sub eliminate spaces
2410 COUNT=LEN(ENTRY$)
2420 IF MID$(ENTRY$,COUNT,1)=" " THEN ENTRY$=MID$(ENTRY$,1,COUNT-1):COUNT=COUNT-1:GOTO 2420
2430 RETURN
