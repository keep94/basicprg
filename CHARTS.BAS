100 INPUT "Starting value for X: ",START
110 INPUT "Incriment: ",INCX
120 INPUT "Decimal places for X: ",DECX
130 INPUT "Decimal places for Y: ",DECY
140 INPUT "Width of X column: ",WIDTHX
150 INPUT "Width of Y column: ",WIDTHY
160 INPUT "Number of rows: ",ROWSIZE
170 INPUT "Number of columns: ",COLSIZE
180 INPUT "Left margin: ",LEFTMAR
200 '  Sub print top
205 LPRINT CHR$(27)+CHR$(88)+CHR$(LEFTMAR)+CHR$(80)
210 PR$ = CHR$(218)
220 PR$=PR$+STRING$(WIDTHX,196)
230 PR$=PR$+CHR$(194)
240 PR$=PR$+STRING$(WIDTHY,196)
250 FOR CNT=2 TO COLSIZE
260 PR$=PR$+CHR$(194)
270 PR$=PR$+STRING$(WIDTHX,196)
280 PR$=PR$+CHR$(194)
290 PR$=PR$+STRING$(WIDTHY,196)
300 NEXT CNT
310 PR$=PR$+CHR$(191)
320 LPRINT PR$
400 '  Sub print middle
410 X=START
420 FOR COUNT=1 TO ROWSIZE
430 GOSUB 700
440 X=X+INCX
450 NEXT COUNT
500 '  Sub print bottom
510 PR$ = CHR$(192)
520 PR$=PR$+STRING$(WIDTHX,196)
530 PR$=PR$+CHR$(193)
540 PR$=PR$+STRING$(WIDTHY,196)
550 FOR CNT=2 TO COLSIZE
560 PR$=PR$+CHR$(193)
570 PR$=PR$+STRING$(WIDTHX,196)
580 PR$=PR$+CHR$(193)
590 PR$=PR$+STRING$(WIDTHY,196)
600 NEXT CNT
610 PR$=PR$+CHR$(217)
620 LPRINT PR$
630 LPRINT CHR$(27)+CHR$(88)+CHR$(1)+CHR$(80)
640 END
700 '   Sub print line
710 TEMP=X
720 PR$=CHR$(179)
730 FOR CNT=1 TO COLSIZE
740 Y=X
742 DE=DECX
747 WID=WIDTHX
750 GOSUB 900
760 PR$=PR$+CHR$(179)
770 GOSUB 1100
780 PR$=PR$+CHR$(179)
790 X=X+ROWSIZE*INCX
800 NEXT CNT
810 LPRINT PR$
820 X=TEMP
830 RETURN
900 '  Sub add x
910 X$=STR$(Y+.5*10^(-DE))
920 POSIT=INSTR(X$,".")
930 IF POSIT=0 THEN 1000
940 IF DE=0 THEN 970
950 IF DE+POSIT>LEN(X$) THEN X$=X$+STRING$(DE+POSIT-LEN(X$),48) ELSE X$=MID$(X$,1,DE+POSIT)
960 GOTO 1020
970 'de = 0
980 X$=MID$(X$,1,POSIT-1)
990 GOTO 1020
1000 ' posit = 0
1010 IF DE>0 THEN X$=X$+"."+STRING$(DE,48)
1020 ' truncate
1030 X$=MID$(X$,2,WID)
1040 X$=STRING$(WID-LEN(X$),32)+X$
1050 PR$=PR$+X$
1060 RETURN
1100 '  Sub add y
1110 GOSUB 1200
1120 WID=WIDTHY
1130 DE=DECY
1140 GOSUB 900
1150 RETURN
1200 '  Sub function
1210 Y=X^2
1220 RETURN
