10 BEGX=0
15 ENDX=0
17 MAXWID=20
20 INV = 0
30 NUMLIN=15
40 DECPL = 4
50 GOSUB 200
60 ON CH GOSUB 500,600,2500,2600,2700,5100
70 GOTO 50
200 '   sub menu
210 CLS
220 LOCATE 1,32
230 PRINT "M A I N   M E N U"
235 LOCATE 4,10
240 PRINT "1.  Change f(x)"
250 LOCATE 6,10
260 IF INV=0 THEN PRINT "2.  View table for f(x)" ELSE PRINT "2.  View table for f-1(x)"
270 LOCATE 8,10
280 IF INV=0 THEN PRINT "3.  Analyze inverse of f(x)" ELSE PRINT "3.  Analyze f(x)"
290 LOCATE 10,10
300 PRINT "4.  Inverse configuration"
310 LOCATE 12,10
320 PRINT "5.  Print chart"
330 LOCATE 14,10
340 PRINT "6.  Quit"
350 LOCATE 16,1
360 INPUT "Pick one: ",CH
370 RETURN
500 '  sub change f(x)
510 EDIT 5020
520 RETURN
600 '  sub view f(x)
605 CLS
610 LOCATE 11,1
620 INPUT "Enter a value for X: ", START
630 LOCATE 13,1
640 INPUT "Enter incriment level: ",INCR
650 GOSUB 700
660 RETURN
700 '  sub display driver
705 MESS=0
710 OFFSET = 0
720 GOSUB 900
730 IF MESS=0 THEN CH$=INKEY$ ELSE MESS=0
740 IF CH$=CHR$(0)+CHR$(72) THEN GOSUB 1300:GOTO 730
750 IF CH$=CHR$(0)+CHR$(80) THEN GOSUB 1500:GOTO 730
760 IF CH$=CHR$(0)+CHR$(73) THEN GOSUB 1100:GOTO 730
770 IF CH$=CHR$(0)+CHR$(81) THEN GOSUB 1200:GOTO 730
780 IF CH$=CHR$(13) THEN GOSUB 1700:GOTO 710
790 IF CH$=CHR$(27) THEN GOSUB 1800:GOTO 710
800 IF CH$=CHR$(24) THEN RETURN
805 IF CH$=CHR$(4) THEN GOSUB 2000:GOSUB 900:GOTO 730
810 IF CH$=CHR$(4) THEN GOSUB 2000:GOSUB 900:GOTO 730
815 IF INV=1 THEN 830
820 IF CH$=CHR$(18) THEN GOSUB 2300:GOTO 730
830 GOTO 730
900 '  sub display
905 CLS
910 LOCATE 1,11:PRINT "X";
920 LOCATE 1,22:PRINT CHR$(179);
930 IF INV=0 THEN LOCATE 1,31:PRINT "f(X)" ELSE LOCATE 1,30:PRINT "f-1(X)"
935 LOCATE 2,2
940 FOR CNT = 1 TO 20:PRINT CHR$(196);:NEXT CNT
950 PRINT CHR$(197);
960 FOR CNT = 1 TO 20:PRINT CHR$(196);:NEXT CNT
965 PRINT
970 FOR CNT = NUMLIN*INT(OFFSET/NUMLIN) TO NUMLIN*INT(OFFSET/NUMLIN)+NUMLIN - 1
975 CH$=INKEY$
977 IF CH$<>"" THEN MESS=1:GOTO 1040
980 XVAL = START + CNT*10^INCR
992 X=XVAL
994 TEMP=DECPL
996 IF INCR < 0 THEN DECPL=-INCR ELSE DECPL=0
998 GOSUB 4800
1000 DECPL=TEMP
1002 XVAL$=X$
1003 GOSUB 5000
1004 X=YVAL
1006 GOSUB 4800
1008 YVAL$=X$
1010 LOCATE ,2
1020 PRINT XVAL$+CHR$(179)+YVAL$
1030 NEXT CNT
1040 LOCATE OFFSET - NUMLIN*INT(OFFSET/NUMLIN) + 3, 1
1050 PRINT CHR$(16);
1060 RETURN
1100 '  sub page up
1110 OFFSET=OFFSET - NUMLIN
1120 GOSUB 900
1130 RETURN
1200 '  sub page down
1210 OFFSET=OFFSET + NUMLIN
1220 GOSUB 900
1230 RETURN
1300 '  sub up
1310 IF OFFSET/NUMLIN = INT(OFFSET/NUMLIN) THEN 1370
1320 LOCATE OFFSET - NUMLIN*INT(OFFSET/NUMLIN) + 3,1
1330 PRINT " ";
1340 OFFSET = OFFSET - 1
1350 LOCATE OFFSET - NUMLIN*INT(OFFSET/NUMLIN) + 3,1
1360 PRINT CHR$(16);
1365 RETURN
1370 '  new screen
1380 OFFSET = OFFSET - 1
1390 GOSUB 900
1400 RETURN
1500 '  sub down
1510 IF (OFFSET+1)/NUMLIN = INT((OFFSET+1)/NUMLIN) THEN 1570
1520 LOCATE OFFSET - NUMLIN*INT(OFFSET/NUMLIN) + 3,1
1530 PRINT " ";
1540 OFFSET = OFFSET + 1
1550 LOCATE OFFSET - NUMLIN*INT(OFFSET/NUMLIN) + 3,1
1560 PRINT CHR$(16);
1565 RETURN
1570 '  new screen
1580 OFFSET = OFFSET + 1
1590 GOSUB 900
1600 RETURN
1700 '  sub ENTER
1710 START = START + OFFSET*10^INCR
1720 INCR = INCR - 1
1730 RETURN
1800 '  sub ESC
1810 X=START+OFFSET*10^INCR
1820 TEMP = DECPL
1830 IF (INCR + 1) < 0 THEN DECPL=-(INCR + 1) ELSE 1890
1840 GOSUB 4800
1850 DECPL=TEMP
1860 START=VAL(X$)
1870 INCR = INCR + 1
1880 RETURN
1890 '  incr + 1 >= 0
1900 START = 10^(INCR + 1)*INT(X/10^(INCR + 1))
1910 INCR = INCR + 1
1920 RETURN
2000 '  sub change decpl
2010 LOCATE NUMLIN+4,1
2020 INPUT "Enter number of decimal places: ",DECPL
2030 RETURN
2100 '  sub find root
2110 XVAL=FIRSTX
2120 GOSUB 5000
2130 FIRSTY=YVAL
2140 XVAL=LASTX
2150 GOSUB 5000
2160 LASTY=YVAL
2170 IF FIRSTY > LASTY THEN SWAP FIRSTY, LASTY:SWAP FIRSTX, LASTX
2180 IF (KEYVAL < FIRSTY) OR (KEYVAL > LASTY) THEN MIDX=0:RETURN
2190 MIDX=(FIRSTX+LASTX)/2
2200 IF (FIRSTX=MIDX) OR (LASTX=MIDX) THEN RETURN
2210 XVAL=MIDX
2220 GOSUB 5000
2230 MIDY=YVAL
2240 IF (KEYVAL >= MIDY) THEN FIRSTX=MIDX:GOTO 2190
2250 LASTX=MIDX:GOTO 2190
2300 '  sub find root driver
2310 LOCATE NUMLIN+4,1:INPUT "f(X) = ",TEMP$
2320 IF TEMP$="" THEN RETURN
2330 KEYVAL=VAL(TEMP$)
2340 FIRSTX=START+OFFSET*10^INCR
2350 LASTX=FIRSTX+10^INCR
2360 GOSUB 2100
2370 LOCATE NUMLIN+6,1:PRINT "X =";MIDX;
2380 INPUT "",TEMP$
2382 LOCATE NUMLIN+4,1:PRINT SPACE$(80);
2384 LOCATE NUMLIN+6,1:PRINT SPACE$(80);
2390 RETURN
2500 '  sub toggle inverse
2510 IF INV=0 THEN INV=1 ELSE INV=0
2520 RETURN
2600 '  sub configure inverse
2610 CLS
2620 LOCATE 11,1:INPUT "Enter first bound for f-1(x): ",BEGX
2630 LOCATE 13,1:INPUT "Enter second bound for f-1(x): ",ENDX
2640 RETURN
2700 '  sub print
2710 CLS
2720 INPUT "Enter starting value for X: ",TEMP$
2723 IF TEMP$="" THEN RETURN
2726 START = VAL(TEMP$)
2730 INPUT "Enter incriment: ",TEMP$
2733 IF TEMP$="" THEN RETURN
2736 INCR = VAL(TEMP$)
2740 INPUT "Enter number of rows: ",TEMP$
2743 IF TEMP$="" THEN RETURN
2746 NUMROWS = VAL(TEMP$)
2750 INPUT "Enter number of columns: ",TEMP$
2753 IF TEMP$="" THEN RETURN
2756 NUMCOLS = VAL(TEMP$)
2760 INPUT "Enter width for X: ",TEMP$
2763 IF TEMP$="" THEN RETURN
2766 WIDTHX = VAL(TEMP$)
2770 IF INV=0 THEN INPUT "Enter width for f(X): ",TEMP$ ELSE INPUT "Enter width for f-1(X): ",TEMP$
2773 IF TEMP$="" THEN RETURN
2776 WIDTHY = VAL(TEMP$)
2780 INPUT "Enter decimal places for X: ",TEMP$
2783 IF TEMP$="" THEN RETURN
2786 DECX = VAL(TEMP$)
2790 IF INV=0 THEN INPUT "Enter decimal places for f(X): ",TEMP$ ELSE INPUT "Enter decimal places for f-1(X): ",TEMP$
2793 IF TEMP$="" THEN RETURN
2796 DECY = VAL(TEMP$)
2800 INPUT "Enter left margin: ",TEMP$
2803 IF TEMP$="" THEN RETURN
2806 LEFTM=VAL(TEMP$)
2810 FOR CNT2=1 TO 5:LPRINT:NEXT CNT2
2820 TOP$=SPACE$(LEFTM)
2830 TOP$=TOP$+CHR$(169)+STRING$(WIDTHX,196)+CHR$(194)+STRING$(WIDTHY,196)
2840 FOR CNT2=1 TO NUMCOLS-1
2850 TOP$=TOP$+CHR$(194)+STRING$(WIDTHX,196)+CHR$(194)+STRING$(WIDTHY,196)
2860 NEXT CNT2
2870 TOP$=TOP$+CHR$(191)
2880 LPRINT TOP$
2883 TEMP1=MAXWID
2886 TEMP2=DECPL
2890 FOR COUNT2=0 TO NUMROWS-1
2900 TOP$=SPACE$(LEFTM)
2910 FOR CNT2=0 TO NUMCOLS-1
2920 X=START+INCR*NUMROWS*CNT2 + COUNT2*INCR
2930 MAXWID=WIDTHX
2940 DECPL=DECX
2950 GOSUB 4800
2960 TOP$=TOP$+CHR$(179)+X$
2970 XVAL=START+INCR*NUMROWS*CNT2 + COUNT2*INCR
2980 GOSUB 5000
2990 X=YVAL
3000 MAXWID=WIDTHY
3010 DECPL=DECY
3020 GOSUB 4800
3030 TOP$=TOP$+CHR$(179)+X$
3040 NEXT CNT2
3050 TOP$=TOP$+CHR$(179)
3060 LPRINT TOP$
3070 NEXT COUNT2
3080 MAXWID=TEMP1
3090 DECPL=TEMP2
3100 BOT$=SPACE$(LEFTM)
3110 BOT$=BOT$+CHR$(192)+STRING$(WIDTHX,196)+CHR$(193)+STRING$(WIDTHY,196)
3120 FOR CNT2=1 TO NUMCOLS-1
3130 BOT$=BOT$+CHR$(193)+STRING$(WIDTHX,196)+CHR$(193)+STRING$(WIDTHY,196)
3140 NEXT CNT2
3150 BOT$=BOT$+CHR$(217)
3160 LPRINT BOT$
3170 LPRINT CHR$(12);
3180 RETURN
4800 '  sub format
4802 NEG = 0
4805 IF X < 0 THEN X = -X:NEG = 1
4808 IF DECPL > 0 THEN X=X+.5*10^-DECPL ELSE X=X+.5
4810 X$=MID$(STR$(INT(X)),2,LEN(STR$(INT(X)))-1)
4820 IF DECPL > 0 THEN X$=X$+"."
4830 FRAC=X - INT(X)
4840 FOR COUNT = 1 TO DECPL
4850 X$=X$+CHR$(48+INT(10*FRAC))
4860 FRAC = 10*FRAC - INT(10*FRAC)
4870 NEXT COUNT
4875 IF NEG = 1 THEN X$="-"+X$
4877 IF LEN(X$)>MAXWID THEN X$=STRING$(MAXWID,"*"):GOTO 4890
4880 X$=STRING$(MAXWID-LEN(X$)," ")+X$
4890 RETURN
5000 '  sub f(x)
5005 IF INV=1 THEN GOSUB 5200:RETURN
5010 X=XVAL
5020 Y=X^X
5030 YVAL=Y
5040 RETURN
5100 '  sub quit
5110 END
5200 '  sub inverse of function
5210 FIRSTX=BEGX
5220 LASTX=ENDX
5230 KEYVAL=XVAL
5240 INV=0
5250 GOSUB 2100
5260 YVAL=MIDX
5270 INV=1
5280 RETURN
