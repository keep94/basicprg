1 DIM M$(12)
2 DIM M(12)
3 DIM A$(651)
10 FOR A=1 TO 12
20 READ M$(A)
30 NEXT A
40 FOR A=1 TO 12
50 READ M(A)
60 NEXT A
65 LET C$(1)=CHR$(218)
66 LET C$(2)=CHR$(179)
67 LET C$(3)=CHR$(195)
68 LET C$(4)=CHR$(192)
70 FOR A=1 TO 6
75 LET C$(1)=C$(1)+STRING$(2,196)+CHR$(194)
76 LET C$(2)=C$(2)+STRING$(2,32)+CHR$(179)
77 LET C$(3)=C$(3)+STRING$(2,196)+CHR$(197)
78 LET C$(4)=C$(4)+STRING$(2,196)+CHR$(193)
80 NEXT A
85 LET C$(1)=C$(1)+STRING$(2,196)+CHR$(191)
86 LET C$(2)=C$(2)+STRING$(2,32)+CHR$(179)
87 LET C$(3)=C$(3)+STRING$(2,196)+CHR$(180)
88 LET C$(4)=C$(4)+STRING$(2,196)+CHR$(217)
100 KEY OFF
110 CLS
120 LOCATE 8,9:PRINT "1.  Edit month"
130 LOCATE 10,9:PRINT "2.  Delete/change year"
140 LOCATE 14,1:PRINT SPACE$(39)
150 LOCATE 14,1:INPUT "Which One: ",A
160 IF A>2 OR A<1 THEN 140
170 IF A=2 THEN GOSUB 1500
200 '   SUB LOAD
210 CLS
220 LOCATE 12,1:INPUT "ENTER MONTH: ",A$
230 LET S$=MID$(A$,1,3)+".MON"
240 OPEN S$ FOR INPUT AS #1
250 INPUT #1,Y
260 FOR A=1 TO 651
270 LINE INPUT #1,A$(A)
275 IF A$(A)="" THEN LET A$(A)=STRING$(40,32)
280 NEXT A
290 CLOSE #1
300 '   SUB CALCULATE
305 LET Y$=STR$(Y)
310 LET Y=Y-1984
315 IF Y-4*INT(Y/4)=0 THEN LET M(2)=1
320 LET Y=Y+INT((Y+3)/4)
330 FOR A=1 TO 12
340 IF M$(A)=A$ THEN 355
350 NEXT A
355 LET L=28+M(A)
360 FOR B=1 TO A-1
370 LET Y=Y+M(B)
380 NEXT B
390 LET Y=Y-7*INT(Y/7)
400 IF M(2)=1 THEN LET M(2)=0
500 '   SUB DISPLAY
501 CLS
502 LOCATE 1,15:PRINT A$+Y$
505 LOCATE 3,10:PRINT "S  M  T  W  T  F  S"
510 LOCATE 4,9:PRINT C$(1)
520 FOR A=1 TO 5
530 LOCATE ,9:PRINT C$(2)
540 LOCATE ,9:PRINT C$(2)
550 LOCATE ,9:PRINT C$(3)
560 NEXT A
570 LOCATE ,9:PRINT C$(2)
580 LOCATE ,9:PRINT C$(2)
590 LOCATE ,9:PRINT C$(4)
600 FOR A=1 TO 40
610 LET D=A-Y
620 IF D<1 OR D>L THEN 740
630 LET D1=INT(D/10)
640 LET D2=D-10*INT(D/10)
650 IF D1=0 THEN LET D1$=" ":GOTO 680
660 LET D1$=STR$(D1)
670 LET D1$=MID$(D1$,2,1)
680 LET D2$=STR$(D2)
690 LET D2$=MID$(D2$,2,1)
700 LET D$=D1$+D2$
710 LET X1=5+3*INT((A-1)/7)
720 LET Y1=7+3*(A-7*INT((A-1)/7))
730 LOCATE X1,Y1:PRINT D$
740 NEXT A
800 '   SUB LIST
810 LOCATE 22,1:INPUT "DAY: ",T$
812 IF MID$(T$,1,1)="Y" THEN GOSUB 2000
814 LET A=VAL(T$)
815 IF A=0 THEN GOSUB 1300
816 IF A>L THEN 810
820 CLS
822 LOCATE 1,13:PRINT A$;A;",";Y$
825 LOCATE 3,1
830 FOR B=A*21-20 TO A*21
840 PRINT A$(B);
850 NEXT B
900 '   sub edit
910 LOCATE 3,1,1
920 LET L$=INKEY$
930 IF L$="" THEN 920
940 IF L$=CHR$(0)+CHR$(72) THEN PRINT CHR$(30);:GOTO 920
950 IF L$=CHR$(0)+CHR$(75) THEN PRINT CHR$(29);:GOTO 920
960 IF L$=CHR$(0)+CHR$(77) THEN PRINT CHR$(28);:GOTO 920
970 IF L$=CHR$(0)+CHR$(80) THEN PRINT CHR$(31);:GOTO 920
973 IF L$=CHR$(8) THEN PRINT CHR$(29)+" "+CHR$(29);:GOTO 920
980 IF L$=CHR$(3) THEN GOSUB 1100
985 IF L$=CHR$(1) THEN 1010
990 PRINT L$;
1000 GOTO 920
1010 CLS
1020 GOTO 500
1100 '   SUB CHANGE
1110 FOR F=21*A-20 TO 21*A
1120 LET A$(F)=""
1130 NEXT F
1150 FOR F=3 TO 23
1160 FOR G=1 TO 40
1170 LET A$(21*A-23+F)=A$(21*A-23+F)+CHR$(SCREEN(F,G))
1180 NEXT G
1190 NEXT F
1200 RETURN 1010
1300 '   SUB SAVE
1310 OPEN S$ FOR OUTPUT AS #1
1315 LET Y=VAL(Y$)
1320 WRITE #1,Y
1330 FOR A=1 TO 651
1340 IF A$(A)=STRING$(40,32) THEN LET A$(A)=""
1350 PRINT #1,A$(A)
1360 NEXT A
1370 CLOSE #1
1380 RETURN 100
1500 '   SUB DELETE/CHANGE YEAR
1510 CLS
1520 LOCATE 12,1:INPUT "DELETE WHAT MONTH (A for all): ",A$
1525 IF A$="QUIT" THEN RETURN 110
1530 LOCATE 12,1:INPUT "CHANGE TO WHAT YEAR? ",Y
1540 IF A$<>"A" THEN 1700
1550 FOR B=1 TO 12
1560 LET S$=MID$(M$(B),1,3)+".MON"
1570 OPEN S$ FOR OUTPUT AS #1
1580 WRITE #1,Y
1590 FOR C=1 TO 651
1600 PRINT #1,""
1610 NEXT C
1620 CLOSE #1
1630 NEXT B
1640 RETURN 100
1700 '   SUB DEL SINGLE MONTH
1710 LET S$=MID$(A$,1,3)+".MON"
1720 OPEN S$ FOR OUTPUT AS #1
1730 WRITE #1,Y
1740 FOR C=1 TO 651
1750 PRINT #1,""
1760 NEXT C
1770 CLOSE #1
1780 GOTO 1500
1790 CLOSE #1
1800 OPEN S$ FOR OUTPUT AS #1
1810 WRITE #1,Y
1820 FOR C=1 TO 651
1830 PRINT #1,A$(C)
1850 NEXT C
1860 CLOSE #1
1870 NEXT B
1880 RETURN 100
2000 '   SUB CHANGE YEAR
2010 LET T1=LEN(T$)
2020 LET Y=VAL(MID$(T$,2,T1-1))
2030 RETURN 305
5000 '   SUB DATA
5010 DATA JANUARY,FEBRUARY,MARCH,APRIL,MAY,JUNE
5020 DATA JULY,AUGUST,SEPTEMBER,OCTOBER,NOVEMBER,DECEMBER
5030 DATA 3,0,3,2,3,2,3,3,2,3,2,3
