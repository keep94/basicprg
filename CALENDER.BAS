10 DIM DAY$(6)
20 DIM MOFF(12)
30 DIM MLEN(12)
100 '   Sub initialize
105 LMARGIN = 16
110 FOR CNT = 0 TO 6
120 READ DAY$(CNT)
130 NEXT CNT
140 FOR CNT = 1 TO 12
150 READ MOFF(CNT)
160 NEXT CNT
162 FOR CNT = 1 TO 12
164 READ MLEN(CNT)
166 NEXT CNT
170 TOP$ = CHR$(218)+STRING$(9,196)
180 FOR CNT = 1 TO 7
190 TOP$ = TOP$+CHR$(194)+STRING$(3,196)
200 NEXT CNT
210 TOP$ = TOP$+CHR$(194)+STRING$(9,196)+CHR$(191)
220 WDIV$ = CHR$(179)+SPACE$(9)+CHR$(195)+STRING$(3,196)
230 FOR CNT = 1 TO 6
240 WDIV$ = WDIV$+CHR$(197)+STRING$(3,196)
250 NEXT CNT
255 WDIVS$ = WDIV$
260 WDIV$ = WDIV$+CHR$(180)+SPACE$(9)+CHR$(179)
270 WDIVS$ = WDIVS$+CHR$(197)+STRING$(9,196)+CHR$(180)
280 MDIV$ = CHR$(198)+STRING$(9,205)
290 FOR CNT = 1 TO 7
300 MDIV$ = MDIV$+CHR$(216)+STRING$(3,205)
310 NEXT CNT
320 MDIV$ = MDIV$+CHR$(216)+STRING$(9,205)+CHR$(181)
330 BOT$ = CHR$(192)+STRING$(9,196)
340 FOR CNT = 1 TO 7
350 BOT$ = BOT$+CHR$(193)+STRING$(3,196)
360 NEXT CNT
370 BOT$ = BOT$+CHR$(193)+STRING$(9,196)+CHR$(217)
500 '  Get input
510 INPUT "Enter year: ",YEAR
520 YEAR = INT(YEAR)
530 IF YEAR > 1900 AND YEAR < 2100 THEN 580
540 PRINT
550 PRINT CHR$(7)+"Invalid year"
560 PRINT
570 GOTO 510
580 GOTO 600
600 '  set up screen
610 KEY OFF
620 CLS
630 LOCATE 12,1:PRINT "Printing for year";YEAR
700 '   print
710 YEAR = YEAR - 1900
720 FOR CNT = 1 TO 6:LPRINT:NEXT CNT
730 FOR CNT1 = 1 TO 3
740 LPRINT SPACE$(LMARGIN)+TOP$
750 FOR CNT2 = 1 TO 4
760 MONTH = (CNT1-1)*4+CNT2
770 GOSUB 1000
780 FOR CNT3 = 1 TO 6
790 LINENO = CNT3
800 GOSUB 1100
810 LPRINT SPACE$(LMARGIN)+NORM$
820 IF CNT3 - 2*INT(CNT3/2) = 1 THEN LPRINT SPACE$(LMARGIN)+WDIV$:GOTO 860
830 IF CNT2 = 4 AND CNT3 - 6*INT(CNT3/6) = 0 THEN LPRINT SPACE$(LMARGIN)+BOT$:GOTO 860
840 IF CNT3 - 6*INT(CNT3/6) = 0 THEN LPRINT SPACE$(LMARGIN)+MDIV$:GOTO 860
850 IF CNT3 - 2*INT(CNT3/2) = 0 THEN LPRINT SPACE$(LMARGIN)+WDIVS$:GOTO 860
860 NEXT CNT3
870 NEXT CNT2
875 IF CNT1 = 3 THEN LPRINT:LPRINT:GOTO 890
880 FOR CNT=1 TO 17:LPRINT:NEXT CNT
890 NEXT CNT1
900 LPRINT SPACE$(LMARGIN)+"Days observed      -->":LPRINT
910 LPRINT SPACE$(LMARGIN)+"Rainy days         -->":LPRINT
920 LPRINT SPACE$(LMARGIN)+"Rainy days / year  -->"
925 LPRINT CHR$(12);
930 CLS
940 END
1000 '   sub find offset
1010 OFFSET = YEAR + INT(YEAR/4) + MOFF(MONTH)
1020 IF (YEAR - 4*INT(YEAR/4) = 0) AND (MONTH < 3) THEN OFFSET = OFFSET - 1
1030 OFFSET = -OFFSET
1040 OFFSET = OFFSET - 7*INT((OFFSET+5)/7)
1050 RETURN
1100 '   sub create line
1110 NORM$ = CHR$(179)+SPACE$(9)
1120 LENGMO = MLEN(MONTH)
1130 IF (YEAR - 4*INT(YEAR/4) = 0) AND (MONTH = 2) THEN LENGMO = LENGMO + 1
1140 FOR CNT4 = 0 TO 6
1150 X = OFFSET+7*LINENO-7+CNT4
1160 IF (X < 1) OR (X >LENGMO) THEN X$=SPACE$(3) ELSE X$=DAY$(CNT4)+MID$(STR$(X),2,LEN(STR$(X))-1)
1165 IF LEN(X$) < 3 THEN X$=MID$(X$,1,1)+"0"+MID$(X$,2,1)
1170 NORM$ = NORM$+CHR$(179)+X$
1180 NEXT CNT4
1190 NORM$ = NORM$+CHR$(179)+SPACE$(9)+CHR$(179)
1200 RETURN
5000 '   sub data
5010 DATA "U","M","T","W","R","F","A"
5020 DATA 0,3,3,6,1,4,6,2,5,0,3,5
5030 DATA 31,28,31,30,31,30,31,31,30,31,30,31
