10 KEY OFF
20 CLS
200 '   sub menu
205 CLS
210 LOCATE 6,10:PRINT "1.  Add fractions"
220 LOCATE 8,10:PRINT "2.  Subtract fractions"
230 LOCATE 10,10:PRINT "3.  Multiply fractions"
240 LOCATE 12,10:PRINT "4.  Divide fractions"
250 LOCATE 14,10:PRINT "5.  Go back to main menu"
260 LOCATE 17,10:INPUT "Pick one: ",CHOICE$
270 IF CHOICE$<"1" OR CHOICE$>"5" THEN 260
280 IF CHOICE$="5" THEN RUN"taymath
305 CLS
310 LOCATE 12,1:INPUT "Enter largest whole part of fraction you want in problem: ",LWHOLE
315 CLS
320 LOCATE 12,1:INPUT "Enter largest denomenator you want in problem: ",LDEN
325 IF LDEN<2 THEN 320
330 IF CHOICE$>"2" THEN 345
335 CLS
340 LOCATE 12,1:INPUT"Enter skill level (1-2): ",SKILL
345 CLS
350 LOCATE 12,1:INPUT "Enter a number between -32767 and 32768: ",RAN
360 RANDOMIZE RAN
400 '   SUB MAIN
410 STIME=TIMER
415 COR=0
420 FOR CNT=1 TO 10
430 IF CHOICE$="1" THEN GOSUB 700
440 IF CHOICE$="2" THEN GOSUB 900
450 IF CHOICE$="3" THEN GOSUB 1100
460 IF CHOICE$="4" THEN GOSUB 1300
465 IF ANS$="done" OR ANS$="DONE" THEN 200
470 NEXT CNT
480 CLS
490 LOCATE 6,1:PRINT "YOUR SCORE: ";COR
500 LOCATE 10,1:PRINT "TIME IT TOOK:";INT(TIMER-STIME-14);" seconds"
510 LOCATE 23,1:PRINT "STRIKE ANY KEY TO CONTINUE"
520 ANS$="":WHILE LEN(ANS$)<1:ANS$=INKEY$:WEND
530 GOTO 200
700 '   sub add
702 CLS
705 PR=1
710 LSDEN=0
720 GOSUB 2000:'get first fraction
725 DEN1=DENOM:NUM1=NUMOR:WHOLE1=WHOLEP:FRACT1$=FRACTN$
730 IF SKILL=1 THEN LSDEN=DEN1
740 GOSUB 2000:'get fraction
750 DEN2=DENOM:NUM2=NUMOR:WHOLE2=WHOLEP:FRACT2$=FRACTN$
760 LOCATE 12,1:PRINT "WHAT IS ";FRACT1$;" + ";FRACT2$;" ? ";
770 INPUT "",ANS$
775 IF ANS$="done" OR ANS$="DONE" THEN RETURN
780 GOSUB 2100:'find answer
790 GOSUB 2300:'evaluate user answer
800 IF NUM4*DEN3=DEN4*NUM3 THEN LOCATE 14,1:PRINT "CORRECT!":COR=COR+PR:TIME=TIMER:WHILE TIMER<TIME+1:WEND:RETURN
810 LOCATE 14,1:PRINT "INCORRECT!, PLEASE TRY AGAIN.":PR=0:TIME=TIMER:WHILE TIMER<TIME+1:WEND:CLS:GOTO 760
900 '   SUB SUBTRACT
902 CLS
905 PR=1
910 LSDEN=0
920 GOSUB 2000:'get first fraction
925 DEN1=DENOM:NUM1=NUMOR:WHOLE1=WHOLEP:FRACT1$=FRACTN$
930 IF SKILL=1 THEN LSDEN=DEN1
940 GOSUB 2000:'get fraction
950 DEN2=DENOM:NUM2=NUMOR:WHOLE2=WHOLEP:FRACT2$=FRACTN$
960 IF (DEN1*(WHOLE2*DEN2+NUM2)>DEN2*(WHOLE1*DEN1+NUM1)) THEN 920
965 LOCATE 12,1:PRINT "WHAT IS ";FRACT1$;" - ";FRACT2$;" ? ";
970 INPUT "",ANS$
975 IF ANS$="done" OR ANS$="DONE" THEN RETURN
980 GOSUB 2200:'find answer
990 GOSUB 2300:'evaluate user answer
1000 IF NUM4*DEN3=DEN4*NUM3 THEN LOCATE 14,1:PRINT "CORRECT!":COR=COR+PR:TIME=TIMER:WHILE TIMER<TIME+1:WEND:RETURN
1010 LOCATE 14,1:PRINT "INCORRECT!, PLEASE TRY AGAIN.":PR=0:TIME=TIMER:WHILE TIMER<TIME+1:WEND:CLS:GOTO 965
1100 '   sub multiply
1102 CLS
1105 PR=1
1110 LSDEN=0
1120 GOSUB 2000:'get first fraction
1125 DEN1=DENOM:NUM1=NUMOR:WHOLE1=WHOLEP:FRACT1$=FRACTN$
1140 GOSUB 2000:'get fraction
1150 DEN2=DENOM:NUM2=NUMOR:WHOLE2=WHOLEP:FRACT2$=FRACTN$
1160 LOCATE 12,1:PRINT "WHAT IS ";FRACT1$;" * ";FRACT2$;" ? ";
1170 INPUT "",ANS$
1175 IF ANS$="done" OR ANS$="DONE" THEN RETURN
1180 GOSUB 2400:'find answer
1190 GOSUB 2300:'evaluate user answer
1200 IF NUM4*DEN3=DEN4*NUM3 THEN LOCATE 14,1:PRINT "CORRECT!":COR=COR+PR:TIME=TIMER:WHILE TIMER<TIME+1:WEND:RETURN
1210 LOCATE 14,1:PRINT "INCORRECT!, PLEASE TRY AGAIN.":PR=0:TIME=TIMER:WHILE TIMER<TIME+1:WEND:CLS:GOTO 1160
1300 '   sub divide
1302 CLS
1305 PR=1
1310 LSDEN=0
1320 GOSUB 2000:'get first fraction
1325 DEN1=DENOM:NUM1=NUMOR:WHOLE1=WHOLEP:FRACT1$=FRACTN$
1340 GOSUB 2000:'get fraction
1350 DEN2=DENOM:NUM2=NUMOR:WHOLE2=WHOLEP:FRACT2$=FRACTN$
1360 LOCATE 12,1:PRINT "WHAT IS ";FRACT1$;" / ";FRACT2$;" ? ";
1370 INPUT "",ANS$
1375 IF ANS$="done" OR ANS$="DONE" THEN RETURN
1380 GOSUB 2500:'find answer
1390 GOSUB 2300:'evaluate user answer
1400 IF NUM4*DEN3=DEN4*NUM3 THEN LOCATE 14,1:PRINT "CORRECT!":COR=COR+PR:TIME=TIMER:WHILE TIMER<TIME+1:WEND:RETURN
1410 LOCATE 14,1:PRINT "INCORRECT!, PLEASE TRY AGAIN.":PR=0:TIME=TIMER:WHILE TIMER<TIME+1:WEND:CLS:GOTO 1360
2000 '   sub produce fraction
2010 WHOLEP=INT(RND*(LWHOLE+1))
2020 IF LSDEN>0 THEN 2040
2030 DENOM=INT(RND*(LDEN-1))+2
2035 GOTO 2050
2040 DENOM=LSDEN
2050 NUMOR=INT(RND*(DENOM-1))+1
2055 IF WHOLEP=0 THEN WHOLE$="":GOTO 2060
2057 WHOLE$=MID$(STR$(WHOLEP),2,LEN(STR$(WHOLEP))-1)
2060 FRACTN$=WHOLE$+" "+MID$(STR$(NUMOR),2,LEN(STR$(NUMOR))-1)+"/"+MID$(STR$(DENOM),2,LEN(STR$(DENOM))-1)
2070 RETURN
2100 '   sub find add answer
2110 NUM1=WHOLE1*DEN1+NUM1
2120 WHOLE1=0
2130 NUM2=WHOLE2*DEN2+NUM2
2140 WHOLE2=0
2150 D1=DEN1:D2=DEN2
2160 GOSUB 3000:'G.C.D
2170 NUM3=NUM1*GCD/DEN1+NUM2*GCD/DEN2
2180 DEN3=GCD
2190 RETURN
2200 '   sub find sub answer
2210 NUM1=WHOLE1*DEN1+NUM1
2220 WHOLE1=0
2230 NUM2=WHOLE2*DEN2+NUM2
2240 WHOLE2=0
2250 D1=DEN1:D2=DEN2
2260 GOSUB 3000:'G.C.D
2270 NUM3=NUM1*GCD/DEN1-NUM2*GCD/DEN2
2280 DEN3=GCD
2290 RETURN
2300 '   sub evaluate user answer
2305 NS=0
2310 PS=INSTR(ANS$," ")
2315 IF PS=0 THEN WHOLE4=0:PS=1:NS=1:GOTO 2330
2320 WHOLE4=VAL(MID$(ANS$,1,PS-1))
2330 PD=INSTR(PS,ANS$,"/")
2335 IF PD=0 THEN NUM4=VAL(ANS$)*DEN3:DEN4=DEN3:RETURN
2340 IF NS=1 THEN NUM4=VAL(MID$(ANS$,1,PD)):GOTO 2360
2350 NUM4=VAL(MID$(ANS$,PS+1,PD-PS-1))
2360 DEN4=VAL(MID$(ANS$,PD+1,LEN(ANS$)-PD))
2370 NUM4=WHOLE4*DEN4+NUM4
2380 RETURN
2400 '   sub find mult answer
2410 NUM1=WHOLE1*DEN1+NUM1
2420 WHOLE1=0
2430 NUM2=WHOLE2*DEN2+NUM2
2440 WHOLE2=0
2450 NUM3=NUM1*NUM2
2460 DEN3=DEN1*DEN2
2470 RETURN
2500 '   sub find div answer
2510 NUM1=WHOLE1*DEN1+NUM1
2520 WHOLE1=0
2530 NUM2=WHOLE2*DEN2+NUM2
2540 WHOLE2=0
2550 NUM3=NUM1*DEN2
2560 DEN3=DEN1*NUM2
2570 RETURN
3000 '   sub G.C.D
3005 D3=D1:D4=D2
3010 IF D2>D1 THEN SWAP D1,D2
3020 D1=D1-D2*INT(D1/D2)
3030 IF D1=0 THEN 3050
3040 GOTO 3010
3050 GCD=D3*D4/D2
3060 RETURN
