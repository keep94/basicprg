10 REM cx,cy are the increment values for x and y axis
20 REM f$ is the name of file
100 DIM G%(49,290)
490 GOSUB 1400
500 '   sub get file
510 KEY OFF
520 CLS
530 LOCATE 12,1:INPUT "Enter file with data to graph: ",F$
540 '   get biggest x and y
550 OPEN F$ FOR INPUT AS #1
560 BX=0:BY=0
565 IF EOF(1) THEN 630
570 LINE INPUT #1,X$
575 IF X$="" THEN 565
580 IF ASC(MID$(X$,1,1))>64 THEN 565
590 IF INSTR(X$," ")=0 THEN PRINT CHR$(7)+"No space between x and y coordinate in file":END
600 IF VAL(MID$(X$,1,INSTR(X$," ")-1))>BX THEN BX=VAL(MID$(X$,1,INSTR(X$," ")-1))
610 IF VAL(MID$(X$,INSTR(X$," ")+1,LEN(X$)-INSTR(X$," ")))>BY THEN BY=VAL(MID$(X$,INSTR(X$," ")+1,LEN(X$)-INSTR(X$," ")))
620 GOTO 565
630 CLOSE #1
700 '   sub get conversion
710 ND=LOG(BX)/LOG(10)+.00126
720 CX=10^(INT(ND)-1)
730 IF ND-INT(ND)<.301 THEN CX=CX*2:GOTO 760
740 IF ND-INT(ND)<.6989 THEN CX=CX*5:GOTO 760
750 CX=CX*10
760 ND=LOG(BY)/LOG(10)+.00126
770 CY=10^(INT(ND)-1)
780 IF ND-INT(ND)<.301 THEN CY=CY*2:GOTO 810
790 IF ND-INT(ND)<.6989 THEN CY=CY*5:GOTO 810
800 CY=CY*10
810 GOTO 900
900 '   sub graph
910 OPEN F$ FOR INPUT AS #1
920 IF EOF(1) THEN 1000
930 LINE INPUT #1,X$
940 IF X$="" THEN X1=-1:GOTO 920
950 IF ASC(MID$(X$,1,1))>64 THEN X1=-1:GOTO 920
960 X=VAL(MID$(X$,1,INSTR(X$," ")-1)):Y=VAL(MID$(X$,INSTR(X$," ")+1,LEN(X$)-INSTR(X$," ")))
970 IF X1=-1 THEN X1=X:Y1=Y:GOTO 920
980 GOSUB 1100
990 X1=X:Y1=Y:GOTO 920
1000 CLOSE #1
1002 PRINT
1003 PRINT
1005 PRINT "X increment:",CX
1006 PRINT
1007 PRINT "Y increment:",CY
1010 GOSUB 1700
1020 END
1100 '   sub draw line
1110 GX1=INT(X1/CX*29+.5)
1120 GY1=INT(Y1/CY*35+.5)
1130 GX=INT(X/CX*29+.5)
1140 GY=INT(Y/CY*35+.5)
1150 CNT1=0
1155 DX=GX-GX1
1160 DY=GY-GY1
1165 PY=GY1
1170 FOR CNT=GX1 TO GX
1180 GOSUB 1300
1190 IF ABS(CNT1)<INT(DX/2)+1 THEN 1230
1200 PY=PY+SGN(DY)
1210 CNT1=CNT1+SGN(DY)*DX
1220 GOTO 1180
1230 CNT1=CNT1-DY
1240 NEXT CNT
1250 RETURN
1300 '   sub plot
1310 LIN=INT(PY/7)
1320 PIN=PY MOD 7
1330 G%(LIN,CNT)=G%(LIN,CNT) OR 2^PIN
1340 RETURN
1400 '   sub initialize
1410 FOR CNT=0 TO 49
1420 G%(CNT,0)=127
1430 NEXT CNT
1440 FOR CNT=1 TO 290
1450 G%(0,CNT)=1
1460 NEXT CNT
1470 FOR CNT=5 TO 45 STEP 5
1480 FOR CNT1=1 TO 7
1490 G%(CNT,CNT1)=1
1500 NEXT CNT1
1510 NEXT CNT
1512 FOR CNT1=1 TO 7
1513 G%(49,CNT1)=64
1514 NEXT CNT1
1520 FOR CNT=29 TO 290 STEP 29
1530 G%(0,CNT)=127
1540 NEXT CNT
1550 FOR CNT=5 TO 45 STEP 5
1560 FOR CNT1=29 TO 290 STEP 29
1570 G%(CNT,CNT1)=3
1580 NEXT CNT1
1590 NEXT CNT
1592 FOR CNT1=29 TO 290 STEP 29
1593 G%(49,CNT1)=96
1594 NEXT CNT1
1600 RETURN
1700 '   sub print
1710 WIDTH "LPT1:",255
1720 FOR CNT=1 TO 12
1730 LPRINT
1740 NEXT CNT
1750 LPRINT CHR$(27)+"1";
1760 FOR CNT=49 TO 0 STEP -1
1770 LPRINT SPACE$(20);
1780 LPRINT CHR$(27)+"K"+CHR$(35)+CHR$(1);
1790 FOR CNT1=0 TO 290
1800 LPRINT CHR$(G%(CNT,CNT1));
1810 NEXT CNT1
1820 LPRINT
1830 NEXT CNT
1840 RETURN
